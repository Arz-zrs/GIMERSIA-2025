extends CanvasLayer

enum Match {PERFECT, OK, MISS}

@export var conductor: Node 
@export var player: Player
@export var note_scene: PackedScene

@export var approach_beats: float = 2.0
var last_spawned_beat: int = 0
var last_processed_beat: int = -1

@onready var center_pos = $CenterSpawnPoint.global_position
@onready var left_spawn_pos = $LeftSpawnPoint.global_position 
@onready var right_spawn_pos = $RightSpawnPoint.global_position

@onready var beat_sprite = $Sprite2D
@onready var right_progress_bar = $right_progress_bar
@onready var left_progress_bar = $left_progress_bar

@onready var left_timing_window = $LTimingWindowIndicator
@onready var right_timing_window = $RTimingWindowIndicator

@onready var beat_label: Label = $BeatLabel

func _ready() -> void:
	if conductor:
		GameStates.beat_hit.connect(_on_beat_hit)
		last_spawned_beat = int(conductor.song_position_in_beats)
		#if conductor.debug_mode:
			#left_timing_window.visible = true
			#right_timing_window.visible = true
	
	right_progress_bar.max_value = 1.0
	left_progress_bar.max_value = 1.0
	beat_label.set_text("")

func _process(_delta: float) -> void:
	if not conductor or not conductor.is_active:
		return
	
	#var beat_progress = fmod(conductor.song_position_in_beats, 1.0)
	#var time_left_visual = 1.0 - beat_progress
	#left_progress_bar.visible = true
	#right_progress_bar.visible = true
	#left_progress_bar.value = time_left_visual
	#right_progress_bar.value = time_left_visual #uncomment this if you want progress bar
	
	if player.is_hopping and player.last_hop_beat != last_processed_beat:
		_validate_player_hit()
	
	_handle_note_spawning()

func _validate_player_hit():
	var target_beat = round(player.last_song_pos)
	
	var diff_beats = abs(player.last_song_pos - target_beat)
	
	var diff_seconds = diff_beats * conductor.sec_per_beat
	
	print("Hit Offset: %.3f sec" % diff_seconds)

	if diff_seconds <= 0.1: 
		AudioAutoloader.playPerfectSound()
		_beat_indicator()
		#print("PERFECT")
		beat_label.set_text("PERFECT")
		player.current_match = Match.PERFECT
	elif diff_seconds <= 0.25:
		_beat_indicator()
		#print("OK")
		beat_label.set_text("OK")
		player.current_match = Match.OK
	else:
		beat_label.set_text("")
		player.current_match = Match.MISS
		#print("Miss (Timing off)")

	last_processed_beat = player.last_hop_beat

func _handle_note_spawning():
	var future_beat = conductor.song_position_in_beats + approach_beats
	
	if int(future_beat) > last_spawned_beat:
		last_spawned_beat = int(future_beat)
		spawn_visual_note(last_spawned_beat)

func spawn_visual_note(target_beat_num: int):
	if not note_scene: return
	
	var note_right = note_scene.instantiate()
	var note_left = note_scene.instantiate()
	add_child(note_right)
	add_child(note_left)
	
	note_right.setup(conductor, target_beat_num, approach_beats, right_spawn_pos, center_pos + Vector2(20, 0))
	note_left.setup(conductor, target_beat_num, approach_beats, left_spawn_pos, center_pos  + Vector2(-20, 0))

func _on_beat_hit(beat_num: int):
	if player.last_hop_beat < beat_num and !GameStates.on_ride_disc:
		GameStates.reset_multiplier()
		_beat_indicator()

func _beat_indicator():
	beat_sprite.frame_coords = Vector2(1, 0)
	
	await get_tree().create_timer(0.1).timeout
	
	beat_sprite.frame_coords = Vector2(0, 0)
